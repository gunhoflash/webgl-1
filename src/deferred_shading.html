<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8" />
		<title>Deferred Shading (WebGL 2.0)</title>
	</head>
	<body onload="main()">
		<h1 align=center>Deferred Shading (WebGL 2.0)</h1>
		<canvas id="webgl" width="512" height="512">
			Please use a browser that supports "canvas"
		</canvas>
		<script id="shader-vert-preproc" type="x-shader/x-vertex">#version 300 es
			layout(location=3) in vec4 aPosition;
			layout(location=7) in vec4 aNormal;
			uniform mat4    MVP;
			uniform mat4	MV;
			uniform mat4	N;
			out vec4	vPosition;
			out	vec3	vNormal;
			void main()
			{
				gl_Position = MVP*aPosition;
				vPosition = MV*aPosition;
				vNormal = normalize((N*vec4(aNormal.xyz, 0)).xyz);
			}
		</script>
		<script id="shader-frag-preproc" type="x-shader/x-fragment">#version 300 es
			#ifdef GL_ES
			precision mediump float;
			#endif
			in vec4	vPosition;
			in	vec3	vNormal;
			uniform vec4	diffuse;
            layout(location=0) out vec4 fPosition;
            layout(location=1) out vec4 fNormal;
            layout(location=2) out vec4 fDiffuse;
			void main()
			{
				fPosition = vPosition;
				fNormal = vec4(normalize(vNormal),1);
				fDiffuse = diffuse;
			}
		</script>
		<script id="vert-tex" type="x-shader/x-vertex">#version 300 es
			layout(location=6) in vec4 aPosition;
			layout(location=9) in vec2 aTexcoord;
			out vec2 vTexcoord;
			void main()
			{
				gl_Position = aPosition;
				vTexcoord = aTexcoord;
			}
		</script>
		<script id="frag-tex" type="x-shader/x-fragment">#version 300 es
			#ifdef GL_ES
			precision mediump float;
			#endif
			in vec2 vTexcoord;
            out vec4 fColor;
			uniform sampler2D tex;
			void main()
			{
				fColor = texture(tex, vTexcoord);
			}
		</script>
		<script id="vert-Phong-Phong" type="x-shader/x-vertex">#version 300 es
			layout(location=6) in vec4 aPosition;
			layout(location=9) in vec2 aTexcoord;
			out vec2 vTexcoord;
			void main()
			{
				gl_Position = aPosition;
				vTexcoord = aTexcoord;
			}
		</script>
		<script id="frag-Phong-Phong" type="x-shader/x-fragment">#version 300 es
			#ifdef GL_ES
			precision mediump float;
			#endif
			in vec2 vTexcoord;
            out vec4 fColor;
			uniform sampler2D	tex_position;
			uniform sampler2D	tex_normal;
			uniform sampler2D	tex_diffuse;
			struct TMaterial
			{
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
				vec3	emission;
				float	shininess;
			};
			struct TLight
			{
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
			};
			TLight		light;
			TMaterial	material;
			uniform vec4	light_position;
			void main()
			{
				vec3	n = texture(tex_normal, vTexcoord).xyz;
				vec4	p = texture(tex_position, vTexcoord);
				
				material.diffuse = texture(tex_diffuse, vTexcoord).rgb;
				material.shininess = 12.8;
				material.specular = vec3(.7);
				material.ambient = vec3(.1);

				light.ambient = vec3(.1);
				light.diffuse = vec3(1);
				light.specular = vec3(1);

				vec3	l;
				if(light_position.w == 1.0)
					l = normalize((light_position - p).xyz);
				else
					l = normalize((light_position).xyz);
				vec3	v = normalize(-p.xyz);

				vec3	r = reflect(-l, n);
				float	l_dot_n = max(dot(l, n), 0.0);

				vec3	ambient = light.ambient * material.ambient;
				vec3	diffuse = light.diffuse * material.diffuse.rgb * l_dot_n;
				vec3	specular = vec3(0.0);
				if(l_dot_n > 0.0)
				{
					specular = light.specular * material.specular * pow(max(dot(r, v), 0.0), material.shininess);
				}
				fColor = vec4(ambient + diffuse + specular, 1);
			}
		</script>
		<script src="../lib/webgl-utils.js"></script>
		<script src="../lib/webgl-debug.js"></script>
		<script src="../lib/cuon-utils.js"></script>
		<script src="../lib/cuon-matrix.js"></script>
		<script src="../resources/monkey_sub2_smooth.js"></script>
		<script src="../resources/sphere.js"></script>
		<script src="deferred_shading.js"></script>
	</body>
</html>
