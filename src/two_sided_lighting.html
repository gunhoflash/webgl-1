<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8" />
		<title>Two-sided lighting (WebGL 2.0)</title>
	</head>

	<body onload="main()">
		<h1 align=center>Two-sided lighting (WebGL 2.0)</h1>
		<canvas id="webgl" width="512" height="512">
			Please use a browser that supports "canvas"
		</canvas>
		<script id="vert-Phong-Phong" type="x-shader/x-vertex">#version 300 es
			layout(location=0) in vec4	aPosition;
			layout(location=1) in vec3	aNormal;
			uniform mat4	MVP;
			uniform mat4	MV;
			uniform mat4	matNormal;
			out vec3	vNormal;
			out vec4	vPosEye;
			void main()
			{
				vPosEye = MV*aPosition;
				vNormal = normalize((matNormal*vec4(aNormal,0)).xyz);
				gl_Position = MVP*aPosition;
			}
		</script>
		<script id="frag-Phong-Phong" type="x-shader/x-fragment">#version 300 es
			#ifdef GL_ES
			precision mediump float;
			#endif
			in vec4 vPosEye;
			in vec3	vNormal;
            out vec4 fColor;
			struct TMaterial
			{
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
				vec3	emission;
				float	shininess;
			};
			struct TLight
			{
				vec4	position;
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
				bool	enabled;
			};
			uniform TMaterial	material_front;
			uniform TMaterial	material_back;
			uniform TLight		light[2];
			void main()
			{
				vec3	n = normalize(vNormal);
				TMaterial	material;
				if(gl_FrontFacing)
				{
					material = material_front;
				}
				else
				{
					material = material_back;
					n = -n;
				}
				vec3	l;
				vec3	v = normalize(-vPosEye.xyz);
				fColor = vec4(0.0);
				for(int i=0 ; i<2 ; i++)
				{
					if(light[i].enabled)
					{
						if(light[i].position.w == 1.0)
							l = normalize((light[i].position - vPosEye).xyz);
						else
							l = normalize((light[i].position).xyz);
						vec3	r = reflect(-l, n);
						float	l_dot_n = max(dot(l, n), 0.0);
						vec3	ambient = light[i].ambient * material.ambient;
						vec3	diffuse = light[i].diffuse * material.diffuse * l_dot_n;
						vec3	specular = vec3(0.0);
						if(l_dot_n > 0.0)
						{
							specular = light[i].specular * material.specular * pow(max(dot(r, v), 0.0), material.shininess);
						}
						fColor += vec4(ambient + diffuse + specular, 1);
					}
				}
				fColor.w = 1.0;
			}
		</script>


		<script src="../lib/webgl-utils.js"></script>
		<script src="../lib/webgl-debug.js"></script>
		<script src="../lib/cuon-utils.js"></script>
		<script src="../lib/cuon-matrix.js"></script>
		<script src="class_mesh.js"></script>
		<script src="class_shader.js"></script>
		<script src="class_light.js"></script>
		<script src="class_material.js"></script>
		<script src="class_axes.js"></script>
		<script src="two_sided_lighting.js"></script>
	</body>
</html>
