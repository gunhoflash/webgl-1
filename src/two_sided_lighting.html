<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8" />
		<title>Two-sided lighting</title>
	</head>

	<body onload="main()">
		<h1 align=center>Two-sided lighting</h1>
		<canvas id="webgl" width="512" height="512">
			Please use a browser that supports "canvas"
		</canvas>
		<script id="vert-axes" type="x-shader/x-vertex">
            attribute vec4 aPosition;
            attribute vec4 aColor;
            uniform mat4 VP;
            varying vec4 vColor;
            void main()
            {
                gl_Position = VP * aPosition;
                vColor = aColor;
            }
		</script>
		<script id="frag-axes" type="x-shader/x-fragment">
            #ifdef GL_ES
            precision mediump float;
            #endif
            varying vec4 vColor;
            void main()
            {
                gl_FragColor = vColor;
            }
		</script>

		<script id="vert-Phong-Phong" type="x-shader/x-vertex">
            // eye coordinate system
			attribute vec4 aPosition;
			attribute vec3 aNormal;
			uniform mat4	MVP;
			uniform mat4	MV;
			uniform mat4	matNormal;
			varying vec3	vNormal;
			varying vec4	vPosWorld;
			void main()
			{
				vPosWorld = MV*aPosition;
				vNormal = normalize((matNormal*vec4(aNormal,0)).xyz);
				gl_Position = MVP*aPosition;
			}
		</script>
		<script id="frag-Phong-Phong" type="x-shader/x-fragment">
            // eye coordinate system
            #ifdef GL_ES
            precision mediump float;
            #endif
			varying vec4 vPosWorld;
			varying vec3	vNormal;
			struct TMaterial
			{
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
				vec3	emission;
				float	shininess;
			};
			struct TLight
			{
				vec4	position;
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
			};
			uniform TMaterial	mat_front;
			uniform TMaterial	mat_back;
			uniform TLight		light;
			void main()
			{
				vec3	n = normalize(vNormal);
				vec3	l;

				TMaterial	mat;

				if(gl_FrontFacing)
				{
					mat = mat_front;
				}
				else
				{
					mat = mat_back;
					n = -n;
				}

				if(light.position.w == 1.0)
					l = normalize((light.position - vPosWorld).xyz);
				else
					l = normalize((light.position).xyz);
				vec3	v = normalize(-vPosWorld.xyz);

				vec3	r = reflect(-l, n);
				float	l_dot_n = max(dot(l, n), 0.0);

				vec3	ambient = light.ambient * mat.ambient;
				vec3	diffuse = light.diffuse * mat.diffuse * l_dot_n;
				vec3	specular = vec3(0.0);
				if(l_dot_n > 0.0)
				{
					specular = light.specular * mat.specular * pow(max(dot(r, v), 0.0), mat.shininess);
				}
				gl_FragColor = vec4(ambient + diffuse + specular, 1);

			}
	
	
		</script>

		<script src="../lib/webgl-utils.js"></script>
		<script src="../lib/webgl-debug.js"></script>
		<script src="../lib/cuon-utils.js"></script>
		<script src="../lib/cuon-matrix.js"></script>
		<script src="two_sided_lighting.js"></script>
	</body>
</html>
