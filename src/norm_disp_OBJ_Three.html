<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8" />
		<title>(Tangent space) normal map + displacement map (OBJ)</title>
	</head>
	<body onload="main()">
		<h1 align=center>(Tangent space) normal map + displacement map (OBJ)</h1>
		<canvas id="webgl" width="1024" height="1024">
			Please use a browser that supports "canvas"
		</canvas>
		<p>
		<input type="checkbox" id="normmap" checked>Use normal map</td>
		<input type="checkbox" id="dispmap" checked>Use displacement map</td>
		</p>
		<p id="output">
		</p>
		<script id="vert-Phong-Phong" type="x-shader/x-vertex">
			attribute vec4	aPosition;
			attribute vec3	aNormal;
			attribute vec2	aTexCoord;
			varying vec2	vTexCoord;
			uniform mat4	MVP;
			uniform mat4	MV;
			uniform mat4	matNormal;
			uniform float	disp_scale;
			uniform float	disp_bias;
			uniform sampler2D tex_disp;
			uniform bool	use_disp_map;
			varying vec3	vNormal;
			varying vec4	vPosEye;
			void main()
			{
				vPosEye = MV*aPosition;
				float	disp = texture2D(tex_disp, aTexCoord).r;
				vec4	p = aPosition;
				if(use_disp_map) p += (disp_scale*disp + disp_bias)*vec4(aNormal, 0);
				vNormal = normalize((matNormal*vec4(aNormal,0)).xyz);
//				vNormal = normalize((vec4(aNormal,0)).xyz);
				gl_Position = MVP*p;
				vTexCoord = aTexCoord;
			}
		</script>
		<script id="frag-Phong-Phong" type="x-shader/x-fragment">
			#extension GL_OES_standard_derivatives : enable
			precision mediump float;
//			uniform sampler2D tex_color;
			uniform highp mat4	matNormal;
			uniform sampler2D tex_norm;
			uniform bool	use_norm_map;
			varying vec4 vPosEye;
			varying vec3	vNormal;
			varying vec2	vTexCoord;
			struct TMaterial
			{
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
				vec3	emission;
				float	shininess;
			};
			struct TLight
			{
				vec4	position;
				vec3	ambient;
				vec3	diffuse;
				vec3	specular;
				bool	enabled;
			};
			uniform TMaterial	material;
			uniform TLight		light[2];

			// courtesy of Three.js fragment shader
			// http://www.thetenthplanet.de/archives/1180
			vec3 perturbNormal2Arb( vec3 eye_pos, vec3 surf_norm ) {
				vec3 q0 = dFdx( eye_pos );
				vec3 q1 = dFdy( eye_pos );
				vec2 st0 = dFdx( vTexCoord );
				vec2 st1 = dFdy( vTexCoord );
				float scale = sign( st1.t * st0.s - st0.t * st1.s );
				vec3 S = normalize( ( q0 * st1.t - q1 * st0.t ) * scale );
				vec3 T = normalize( ( - q0 * st1.s + q1 * st0.s ) * scale );
				vec3 N = normalize( surf_norm );
				mat3 tsn = mat3( S, T, N );
				vec3 mapN = texture2D( tex_norm, vTexCoord ).xyz * 2.0 - 1.0;
				mapN.xy *= ( float( gl_FrontFacing ) * 2.0 - 1.0 );
				return normalize( tsn * mapN );
			}

			// http://www.thetenthplanet.de/archives/1180
			mat3 cotangent_frame( vec3 N, vec3 p, vec2 uv )
			{
				// get edge vectors of the pixel triangle
				vec3 dp1 = dFdx( p );
				vec3 dp2 = dFdy( p );
				vec2 duv1 = dFdx( uv );
				vec2 duv2 = dFdy( uv );
				
				// solve the linear system
				vec3 dp2perp = cross( dp2, N );
				vec3 dp1perp = cross( N, dp1 );
				vec3 T = dp2perp * duv1.x + dp1perp * duv2.x;
				vec3 B = dp2perp * duv1.y + dp1perp * duv2.y;
				
				// construct a scale-invariant frame 
				float invmax = inversesqrt( max( dot(T,T), dot(B,B) ) );
				return mat3( T * invmax, B * invmax, N );
			}

			vec3 perturb_normal( vec3 N, vec3 V, vec2 texcoord )
			{
				// assume N, the interpolated vertex normal and 
				// V, the view vector (vertex to eye)
//				vec3 map = texture2D( mapBump, texcoord ).xyz;
				vec3 map = 2.0*texture2D( tex_norm, texcoord ).xyz - 1.0;
				mat3 TBN = cotangent_frame( N, -V, texcoord );
				return normalize( TBN * map );
			}

			void main()
			{
				vec3	n;
				n = normalize(vNormal);
				vec3	v = normalize(-vPosEye.xyz);
				if(use_norm_map)
				{
//					n = normalize((matNormal * vec4(n, 0)).xyz);
					n = perturbNormal2Arb(v, n);
//					n = normalize(perturb_normal(n, v, vTexCoord));
				}
				vec3	l;
				gl_FragColor = vec4(0.0);
				for(int i=0 ; i<2 ; i++)
				{
					if(light[i].enabled)
					{
						if(light[i].position.w == 1.0)
							l = normalize((light[i].position - vPosEye).xyz);
						else
							l = normalize((light[i].position).xyz);
						vec3	r = reflect(-l, n);
						float	l_dot_n = max(dot(l, n), 0.0);
						vec3	ambient = light[i].ambient * material.ambient;
						vec3	diffuse = light[i].diffuse * material.diffuse * l_dot_n;
						vec3	specular = vec3(0.0);
						if(l_dot_n > 0.0)
						{
							specular = light[i].specular * material.specular * pow(max(dot(r, v), 0.0), material.shininess);
						}
						gl_FragColor += vec4(ambient + diffuse + specular, 1);
					}
				}
				gl_FragColor.w = 1.0;
//				gl_FragColor = vec4(n, 1);
			}
		</script>


		<script src="../lib/webgl-utils.js"></script>
		<script src="../lib/webgl-debug.js"></script>
		<script src="../lib/cuon-utils.js"></script>
		<script src="../lib/cuon-matrix.js"></script>
		<script src="https://threejs.org/build/three.min.js"></script>
		<script src="https://threejs.org/examples/js/loaders/OBJLoader.js"></script>
		<script src="https://threejs.org/examples/js/loaders/GLTFLoader.js"></script>
		<script src="class_material.js"></script>
		<script src="class_shader.js"></script>
		<script src="class_light.js"></script>
		<script src="class_mesh.js"></script>
		<script src="class_axes.js"></script>
		<script src="class_texture.js"></script>
		<script src="norm_disp_OBJ_Three.js"></script>
	</body>
</html>
